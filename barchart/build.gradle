buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'BarChart'          //the repo name in Bintray dashboard
    bintrayName = 'com.widget.barchart'//the package name in Bintray repo
    libraryName = 'barchart'           // the module name in the android studio project
    //library name will be like -> publishedGroupId:artifact:libraryVersion
    publishedGroupId = 'com.widget'             //the ID you want to add in the 'implementation line'
    artifact = 'barChart'               //the artifact you want to add in the 'implementation line'
    libraryVersion = '2.1.1'
    libraryDescription = 'An Android library that lets you create ChartView in which you can set your bar and column numbers and manage colorsand gap between them in a simple and fast way.'

    siteUrl = 'https://github.com/ma7madfawzy/BarChart'
    gitUrl = 'https://github.com/ma7madfawzy/BarChart.git'


    developerId = 'ma7madfawzy'
    developerName = 'Mahmad fawzy'
    developerEmail = 'ma7madfawzy@gmail.com'

    licenseName = 'The Apache Software License, Version 1.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-1.0.txt'
    allLicenses = ["Apache-2.0"]
}
android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "2.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.0'
    testImplementation 'junit:junit:4.12'
    // RxJava math
    implementation 'io.reactivex:rxjava-math:1.0.0'
    implementation 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'me.grantland:autofittextview:0.2.+'
    implementation 'com.intuit.sdp:sdp-android:1.0.+'
    implementation 'com.intuit.ssp:ssp-android:1.0.+'
    implementation 'tk.zielony:carbon:0.15.2'
}
group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
//this line makes it easier to make uploading to Bintray
if(project.rootProject.file('local.properities').exists()){
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
}

